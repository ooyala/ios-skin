apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion rootProject.compileSdkVersion

    compileOptions {
        sourceCompatibility rootProject.javaVersion
        targetCompatibility rootProject.javaVersion
    }

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warning 'InvalidPackage'
    }
}

def sdkName = 'skin'
ext {
    bintrayRepo = project.bintrayRepo
    bintrayName = sdkName

    publishedGroupId = project.publishedGroupId
    libraryName = sdkName
    artifact = sdkName

    libraryDescription = 'Ooyala Skin SDK'

    libraryVersion = project.libraryVersion
}
group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                developers {
                    developer {
                        id project.developerId
                        name project.developerName
                        email project.developerEmail
                    }
                }
            }
        }
    }
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

artifacts {
//    archives javadocJar
    archives sourcesJar
}

// Bintray
bintray {
    user = project.bintrayUser
    key = project.bintrayApiKey

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}

repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
    google()
}

task copyLibs(type: Copy) {
    from new File(projectDir, '../../../vendor/')
    from new File(projectDir, '../../../vendor/Ooyala/OoyalaSDK-Android/OoyalaSDK.aar')
    from new File(projectDir, '../../../vendor/Facebook/react-native-0.58.0.aar')
    into new File(projectDir, './libs')
    doLast {
        println "Libs updated."
    }
}

tasks.copyLibs.execute()

def supportLibraryVersion = '27.1.1'

project.configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.android.support'
                && !details.requested.name.contains('multidex')) {
            details.useVersion "${supportLibraryVersion}"
        }
    }
}

dependencies {
    implementation files('libs/OoyalaSDK.aar')
    // implementation project(':core')
    // implementation files(rootProject.files('app/libs/OoyalaSDK.aar'))

    // App dependencies
    implementation "androidx.appcompat:appcompat:$rootProject.supportLibraryVersion"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.supportLibraryVersion"
    implementation "androidx.recyclerview:recyclerview:$rootProject.supportLibraryVersion"
    implementation "com.fasterxml.jackson.core:jackson-core:$rootProject.jacksonVersion"
    implementation "com.google.android.exoplayer:exoplayer:$rootProject.exoplayerVersion"
    implementation "javax.inject:javax.inject:$rootProject.injectVersion"

    //react-native dependencies
    implementation "com.facebook.infer.annotation:infer-annotation:$rootProject.inferAnnotationVersion"
    implementation "com.facebook.fresco:fresco:$rootProject.frescoVersion"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$rootProject.frescoVersion"
    implementation "com.facebook.soloader:soloader:$rootProject.soloaderVersion"
    implementation "com.google.code.findbugs:jsr305:$rootProject.findbugsVersion"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$rootProject.okhttpVersion"
    implementation "com.squareup.okio:okio:$rootProject.okioVersion"
    implementation "org.webkit:android-jsc:$rootProject.androidJscVersion"
    implementation(group:'com.facebook', name:'react-native', version:'0.58.0', ext:'aar')
}